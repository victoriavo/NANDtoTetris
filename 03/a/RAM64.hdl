// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Put your code here:

    //We can use the most significant bits from the address
    //to choose which register range the given address wants to access
    DMux8Way(in=load, sel=address[3..5],
    a=regs0, b=regs1, c=regs2, d=regs3,
    e=regs4, f=regs5, g=regs6, h=regs7); //a=000, b=111

    //This will load a value into the memory addresses if correct register range
    //The specific register will be chosen using the least significant bits
    RAM8(in=in, load=regs0, address=address[0..2], out=out0); //Address: 000 000 - 000 111 (Registers 0-7);
    RAM8(in=in, load=regs1, address=address[0..2], out=out1); //Address: 001 000 - 001 111 (Registers 8-15);
    RAM8(in=in, load=regs2, address=address[0..2], out=out2); //Address: 010 000 - 010 111 (Registers 16-23);
    RAM8(in=in, load=regs3, address=address[0..2], out=out3); //Address: 011 000 - 011 111 (Registers 24-31);

    RAM8(in=in, load=regs4, address=address[0..2], out=out4); //Address: 100 000 - 100 111 (Registers 32-39);
    RAM8(in=in, load=regs5, address=address[0..2], out=out5); //Address: 101 000 - 101 111 (Registers 40-47);
    RAM8(in=in, load=regs6, address=address[0..2], out=out6); //Address: 110 000 - 110 111 (Registers 48-55);
    RAM8(in=in, load=regs7, address=address[0..2], out=out7); //Address: 111 000 - 111 111 (Registers 56-63);

    //Now we output the loaded value
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, sel=address[3..5], out=out);
}
